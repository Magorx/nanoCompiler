{
	func printn_arr[var arr = arr][var size = size] {
		var i = 0;
		>| (i < size - 1) {
			__PUT_NUMBER__ arr[i];
			__PUT_CHAR__ ' ';
			i = i + 1;
		}
		__PUT_NUMBER__ arr[i];
	}

	func _n __PUT_NUMBER__; /* declaring 1-line function without {}! */

	func buble_sort[var arr = arr][var size = size] {
		>> (var i = 0 | i < size * size | i = i + 1) { /* for loop: (initialization | condition | step) */
			var flag = 0;
			>> (var j = 0 | j < size - 1 | j = j + 1) {
				? (arr[j] > arr[j + 1]) {
					var tmp = arr[j];
					arr[j] = arr[j + 1];
					arr[j + 1] = tmp;
					flag = 1;
				}
			}
			? (flag == 0) ret;
		}
	}

	func main_sort {
		var n   =  @; /* @ leaves user input */
		var arr = #n; /* malloc n doubles    */

		>> (var i = 0 | i < n | i = i + 1) arr[i] = @; /* read the array */

		buble_sort[arr][n]; /* sort it */

		printn_arr[arr][n];
		_n;
	}

	main_sort;
}